plugins {
    id 'java-library'
    id 'distribution'
}

group 'org.citydb.ade'
version '1.3.0-SNAPSHOT'
description 'Energy ADE extension for the 3DCityDB'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

configurations {
    citygml4j
}

repositories {
    maven {
        url 'https://citydb.jfrog.io/artifactory/maven'
    }
    maven {
        url 'https://repo.osgeo.org/repository/release'
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    mavenCentral()
}

dependencies {
    api 'org.citydb:impexp-client:4.3.0-rc5'
    api 'org.citygml4j.ade:energy-ade-citygml4j:1.0.3-SNAPSHOT'
    
    citygml4j('org.citygml4j.ade:energy-ade-citygml4j:1.0.3-SNAPSHOT') {
        transitive = false
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

distributions.main {
    distributionBaseName = 'energy-ade'

    contents {
        from 'README.md'
        into('lib') {
            from jar
            from configurations.citygml4j
        }
        into('schema-mapping') {
            from "$rootDir/resources/database/schema-mapping"
        }
        into('3dcitydb') {
            from "$rootDir/resources/database/3dcitydb"
        }
    }
}