plugins {
    id 'java-library'
    id 'distribution'
}

group 'org.citydb.ade'
version '1.2.0'
description 'Energy ADE extension for the 3DCityDB'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

configurations {
    citygml4j
}

repositories {
    maven {
        url 'https://dl.bintray.com/3dcitydb/maven'
    }
    maven {
        url 'https://repo.osgeo.org/repository/release'
    }
    jcenter()
}

dependencies {
    api 'org.citydb:impexp-client:4.2.3'
    api('org.citygml4j.ade:energy-ade-citygml4j:1.0.0')
    
    citygml4j('org.citygml4j.ade:energy-ade-citygml4j:1.0.0') {
        transitive = false
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

distributions.main {
    distributionBaseName = 'energy-ade'
    
    contents {
        from 'README.md'
        into('lib') {
            from jar
            from configurations.citygml4j
        }
        into('schema-mapping') {
            from "$rootDir/resources/database/schema-mapping"
        }
        into('3dcitydb') {
            from "$rootDir/resources/database/3dcitydb"
        }
    }
}