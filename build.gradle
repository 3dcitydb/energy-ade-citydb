plugins {
    id 'java-library'
    id 'distribution'
}

group 'org.citydb.ade'
version '1.3.0'
description 'Energy ADE extension for the 3DCityDB'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    compileJava {
        options.release = 8
    }
}

configurations {
    citygml4j
}

repositories {
    maven {
        url 'https://citydb.jfrog.io/artifactory/maven'
    }
    maven {
        url 'https://repo.osgeo.org/repository/release'
    }
    mavenCentral()
}

dependencies {
    api 'org.citydb:impexp-client-gui:4.4.0-SNAPSHOT'
    api 'org.citygml4j.ade:energy-ade-citygml4j:1.0.3'
    
    citygml4j('org.citygml4j.ade:energy-ade-citygml4j:1.0.3') {
        transitive = false
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

distributions.main {
    distributionBaseName = 'energy-ade'

    contents {
        from 'README.md'
        into('lib') {
            from jar
            from configurations.citygml4j
        }
        into('schema-mapping') {
            from "$rootDir/resources/database/schema-mapping"
        }
        into('3dcitydb') {
            from "$rootDir/resources/database/3dcitydb"
        }
    }
}